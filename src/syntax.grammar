@top Program { expression* }

@skip { space | LineComment }

Form { "(" expression* ")" }
Tuple { "[" expression* "]" }
Struct { "{" expression* "}" }
Array { "@[" expression* "]" }
Table { "@{" expression* "}" }

expression {
  Identifier { identifier } |
  Number { number } |
  Boolean { @specialize<identifier, "true" | "false"> } |
  Nil { @specialize<identifier, "nil"> } |
  Special {
    @specialize<identifier,
    "import" | "import*" | "use" |
    "let" |
    "with" | "with-dyns" | "with-syms" | "with-vars" |
    "default" |
    "set" | "setdyn" |
    "var" | "var-" | "varfn" | "varglobal" |
    "def" | "def-" | "defdyn" | "defglobal" |
    "defn" | "defn-" |
    "fn" | "short-fn" |
    "defmacro" | "defmacro-" |
    "if" | "if-not" | "if-let" | "if-with" |
    "when" | "when-let" | "when-with" |
    "unless" | "cond" |
    "case" | "match" |
    "while" | "for" | "forv" | "break" |
    "loop" | "seq" | "tabseq" | "generate" |
    "each" | "eachk" | "eachp" |
    "coro" | "yield" |
    "try" |
    "label" | "return" |
    "macex" | "macex1"
    >
  } |
  String |
  Pipe | Comma | Semicolon | Quote | Quasiquote |
  Keyword |
  Form | Tuple | Struct | Array | Table
}

@external tokens backtickString from "./tokens" { backtickQuotedString }
String { doubleQuotedString | backtickQuotedString }

@tokens {
  // identifiers can't start with : or a number
  leadingIdentifier { $[a-zA-Z!?$&*+\-./<=>@^_] }
  // but they can contain them
  interiorIdentifier { $[a-zA-Z0-9!?$&*+\-./:<=>@^_] }
  identifier { leadingIdentifier interiorIdentifier* }

  janetDigit { @digit | "_" }
  number { $[+-]? (@digit janetDigit* ("." janetDigit*)? | "." @digit janetDigit*) ("e" @digit+)? }

  doubleQuotedString { '"' (!["\\] | "\\" _)* '"' }

  // note that keywords *can* start with a number
  Keyword { ":" interiorIdentifier* }

  Pipe { "|" }
  Comma { "," }
  Semicolon { ";" }
  Quasiquote { "~" }
  Quote { "'" }

  LineComment { "#" ![\n]* }

  space { $[ \t\n\r]+ }

  "(" ")"
  "[" "]"
  "{" "}"
  "@{"
  "@["
  @precedence {"@{", identifier}
  @precedence {"@[", identifier}
  @precedence {number, identifier}
}

@detectDelim

